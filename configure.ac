dnl Autoconf file for the libquadtree project
dnl Copyright (C) 2011-2012 Lokku ltd. and contributors
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License along
dnl with this program; if not, write to the Free Software Foundation, Inc.,
dnl 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

dnl Autoconf 2.68 required to build
AC_PREREQ([2.68])

dnl Version number of source code
AC_INIT([libquadtree], [1.1], [opensource@lokku.com])

dnl Version number of library (NOT the package version)
dnl >>>Read note 5 in README.maintainer before updating<<<
LIB_API_VERSION=1  # Increment when the API changes (0-indexed)
LIB_REVISION=0     # Implementation version of current API (0-indexed)
LIB_AGE=1          # Number of old APIs this API is backwards-compatible with
QUADTREE_LIB_VERSION="${LIB_API_VERSION}:${LIB_AGE}:${LIB_REVISION}"
AC_SUBST(QUADTREE_LIB_VERSION)

dnl "Parts" indicates parts of the generated configure file
AC_COPYRIGHT([Parts Copyright (C) Lokku ltd. and contributors])

dnl Eliminate clutter with auxiliary build directory
AC_CONFIG_AUX_DIR([build])
AC_CONFIG_MACRO_DIR([build/m4])

dnl These are automake options NOT gcc CFLAGS
AM_INIT_AUTOMAKE([-Wall -Werror dist-bzip2])

dnl The name of any source file, for autoconf to do a sanity check
AC_CONFIG_SRCDIR([src/quadtree.c])

dnl config.h is generated by autoconf
AC_CONFIG_HEADERS([src/config.h])

dnl List of makefiles
AC_CONFIG_FILES([Makefile
                 src/Makefile
                 docs/Makefile])

dnl /usr/local doesn't work on some distros I tested,
dnl  and MANPATH doesn't always include /usr/local/share/man/
AC_PREFIX_DEFAULT([/usr])

dnl Programs
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL

dnl Initialize libtool
LT_INIT

dnl Check for standard headers
AC_HEADER_STDC

dnl Types
AC_TYPE_SIZE_T
AC_TYPE_UINT64_T
AC_TYPE_UNSIGNED_LONG_LONG_INT


dnl See if we have bits/wordsize.h, or find the wordsize
AC_CHECK_HEADERS([bits/wordsize.h],
                 [],
                 [],
                 [#include <bits/wordsize.h>
                 ])
AC_LANG_PUSH([C])
if test x"$cross_compiling" = x"no" ; then
   AC_MSG_CHECKING([for the wordsize])
fi
AC_RUN_IFELSE([AC_LANG_PROGRAM([[]],
                  [[if (sizeof(void *) == 16) {
                        return 16;
                    } else if (sizeof(void *) == 32) {
                        return 32;
                    } else if (sizeof(void *) == 64) {
                        return 64;
                    }
                  ]])],
                  [WORDSIZE=32], dnl never reached
                  [WORDSIZE=$?],
                  [WORDSIZE=32]) dnl Assume 32-bit for cross-compiling
AC_LANG_POP([C])
if test x"$cross_compiling" = x"no" ; then
   AC_MSG_RESULT([$WORDSIZE])
fi
AC_DEFINE_UNQUOTED(MACHINE_WORDSIZE, [$WORDSIZE], [Word size of the machine])

dnl See if we support long long unsigned's and their printing
AC_LANG_PUSH([C])
AC_MSG_CHECKING([whether printf supports the llu format specifier])
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
                #include <stdio.h>
                #include <string.h>
              ]],
              [[char out[10];
                int ret = sprintf(out, "%llu", (unsigned long long)1984);
                if (ret < 0 || strcmp(out, "1984")) {
                   return 1;
                }
              ]])],
              [HAS_LLU=1],
              [HAS_LLU=0],
              [HAS_LLU=0]) # Assume we don't have it for cross-compiling
AC_LANG_POP([C])
if test x"$HAS_LLU" == x"1" ; then
   AC_MSG_RESULT([yes])
   AC_DEFINE(HAVE_PRINTF_LLU, [1], [Supports printf %llu specifier])
else
   AC_MSG_RESULT([no])
fi

dnl And we're done
AC_OUTPUT

dnl Output a success message
if test x"$silent" != x"yes" ; then
   echo "----------------------------------------------------------"
   echo "Package configuration was successful, type 'make' to build"
   echo "----------------------------------------------------------"
fi

